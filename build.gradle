import java.time.OffsetDateTime

buildscript {
    ext {
        kotlin_version = '1.2.71'
        vertx_jooq_version = '4.0.0'
        postgresql_version = '42.2.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.postgresql:postgresql:$postgresql_version"
    }
}

group = 'com.swanpipe'
description ="""
SwanPipe Backend:${project.name}"""

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "org.slf4j:slf4j-jdk14:1.7.25"
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-rx-java2:$vertx_version"
    compile "io.reactiverse:reactive-pg-client:0.10.6"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"
    compile "com.swanpipe:swanpipe_flyway:0.0.1-SNAPSHOT"
    compile "com.swanpipe:swanpipe_jooq:0.0.1-SNAPSHOT"
    
    testCompile "io.vertx:vertx-junit5:$vertx_version"
    testCompile "org.assertj:assertj-core:3.8.0"
    // the jupiter-engine version needs to be kept in line with the vertx-junit5 dependency on junit
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.3"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

static def getDate() {
    return OffsetDateTime.now()
}

processResources {
    expand(version: version, buildDate: getDate() )
}

jar {
    /**
     *  Create a fat jar
     */
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree( it ) }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

