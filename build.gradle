/*
 * Copyright (c) 2018. Andrew Newton
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License. 
 */

import java.time.OffsetDateTime

buildscript {
    ext {
        kotlin_version = '1.3.10'
        postgresql_version = '42.2.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.postgresql:postgresql:$postgresql_version"
    }
}

group = 'com.swanpipe'
description ="""
SwanPipe Backend:${project.name}"""

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

configurations {
    compile {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre8'
    }
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    
    // Logging
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "org.slf4j:slf4j-jdk14:1.7.25"
    
    // Vertx
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-rx-java2:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-shell:$vertx_version"
    compile "io.vertx:vertx-auth-shiro:$vertx_version"
    compile "io.vertx:vertx-web-api-contract:$vertx_version"
    compile "io.vertx:vertx-auth-jwt:$vertx_version"
    compile "io.vertx:vertx-web-client:$vertx_version"
    
    // Reactive PG Client
    compile "io.reactiverse:reactive-pg-client:0.10.7"
    
    // Jackson Kotlin module
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    compile 'org.mindrot:jbcrypt:0.4'

    compile "com.markodevcic.kvalidation:KValidation:1.0.0"

    // https://mvnrepository.com/artifact/com.opentable.components/otj-pg-embedded
    testCompile group: 'com.opentable.components', name: 'otj-pg-embedded', version: '0.12.4'

    testCompile "org.flywaydb:flyway-core:5.2.1"
    
    testCompile "io.vertx:vertx-junit5:$vertx_version"
    testCompile "org.assertj:assertj-core:3.8.0"
    // the jupiter-engine version needs to be kept in line with the vertx-junit5 dependency on junit
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.2.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

static def getDate() {
    return OffsetDateTime.now()
}

processResources {
    filesMatching('version.json') {
        expand(version: version, buildDate: getDate() )
    }
}

jar {
    manifest {

        /**
         * Points to our CLI
         */
        attributes 'Main-Class': "com.swanpipe.CliKt"

    }
    
    /**
     *  Create a fat jar
     */
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree( it ) }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

